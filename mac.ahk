;================================================================
;auto hot key script for windows
;mac風キーバインド
;================================================================
#InstallKeybdHook

;================================================================
;関数
;================================================================
;----------------------------------------------------------------
;キーバインドを無効にするウィンドウかどうか判断する
; ahk_classはauto hot key付属のAU3_Spy.exeを使用して調べる。
;  引数 なし
;  戻り値 1:キーバインドを無効にするウィンドウ
;         0:キーバインドを有効にするウィンドウ
;----------------------------------------------------------------
is_disable_window()
{
	;gvim
	IfWinActive,ahk_class Vim
	{
		return 1
	}
	;tera term
	IfWinActive,ahk_class VTWin32
	{
		return 1
	}
	;vmware player
	IfWinActive,ahk_class VMPlayerFrame
	{
		return 1
	}
	return 0
}
;----------------------------------------------------------------
;キーを送信する
; キーバインドを無効にするウィンドウでは、送信されたキーをそのまま使用する
; キーバインドを有効にするウィンドウでは、送信されたキーを置き換える
;
;  引数 original_key:キーバインドを無効にするウィンドウの場合、送信するキー
;       replace_key:キーバインドを有効にするウィンドウの場合、送信するキー
;  戻り値 なし
;----------------------------------------------------------------
send_key(original_key,replace_key)
{
	if (is_disable_window())
	{
		Send,%original_key%
		return
	}
	Send,%replace_key%
	return
}
;================================================================
;ctrlキーバインド
;================================================================
;----------------------------------------------------------------
;移動系（shiftキーとの同時押し対応）
;ctrl + a : Home
;ctrl + e : End
;----------------------------------------------------------------

$<^a::send_key("^a","{Home}")
$<^+a::send_key("^+a","+{Home}")
$<^e::send_key("^e","{End}")
$<^+e::send_key("^+e","+{End}")

;----------------------------------------------------------------
;編集系
;ctrl + h : BackSpace
;ctrl + d : Delete
;ctrl + k : カタカナ変換
;ctrl + j : ひらがな変換
;----------------------------------------------------------------
$<^k::send_key("^k", "{F7}")
$<^j::send_key("^j", "{F6}")
$<^h::send_key("^h", "{BS}")
$<^d::send_key("^d", "{Del}")


;----------------------------------------------------------------
;編集系(Mac like)
;Alt + 左右で単語移動
;Alt + Backspaceで単語削除
;----------------------------------------------------------------

$!BackSpace::send_key("!Backspace", "^{Backspace}")
$!Right::send_key("!Right","^{Right}")
$!Left::send_key("!Left","^{Left}")
$!+Right::send_key("!Right","^+{Right}")
$!+Left::send_key("!Left","^+{Left}")